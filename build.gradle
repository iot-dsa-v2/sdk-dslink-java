subprojects {

    apply plugin: 'jacoco'
    apply plugin: 'java-library'
    apply plugin: 'maven'

    group 'org.iot-dsa'
    version '0.72.1'

    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        jcenter()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    jar {
        manifest {
            attributes(
                    'Automatic-Module-Name': 'org.iot.dsa.' + project.name.replaceAll('-', '.')
            )
        }
    }

    task sourcesJar(group: 'build', type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    test {
        useTestNG()
    }

    jacocoTestReport {

        reports {
            xml.enabled true
            csv.enabled false
            html.enabled true
        }
    }

    build.dependsOn jacocoTestReport

}

allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

wrapper {
    gradleVersion = '5.6.2'
}
