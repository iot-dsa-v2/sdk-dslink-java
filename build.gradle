subprojects {

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group 'org.iot-dsa'
    version '0.77.0'

    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    jar {
        manifest {
            attributes(
                    'Automatic-Module-Name': 'org.iot.dsa.' + project.name.replaceAll('-', '.')
            )
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    task sourcesJar(group: 'build', type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    test {
        useTestNG()
    }

}

allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

wrapper {
    gradleVersion = '6.8'
}
