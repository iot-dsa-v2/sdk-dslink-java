subprojects {

    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'maven'

    group 'org.iot-dsa'
    version '0.44.0.1'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenLocal()
        mavenCentral()
    }

    task sourcesJar(group: 'build', type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    jacocoTestReport {

        reports {
            xml.enabled true
            csv.enabled false
            html.enabled true
        }
    }

    build.dependsOn jacocoTestReport

}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

wrapper {
    gradleVersion = '4.10.2'
}
