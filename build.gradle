allprojects {

    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'maven'

    group 'org.iot-dsa'
    version '0.41.0.1'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenLocal()
        mavenCentral()
    }

    task sourcesJar(group: 'build', type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    jacocoTestReport {

        reports {
            xml.enabled false
            csv.enabled false
            html.enabled false
        }
    }

    build.dependsOn jacocoTestReport
    //test.finalizedBy(project.tasks.jacocoTestReport)

}

task jacocoRootReport(type: JacocoReport) {

    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }

}

wrapper {
    gradleVersion = '4.10.2'
}
